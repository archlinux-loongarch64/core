# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Stéphane Gaudreault <stephane@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=libffi
pkgver=3.4.2
pkgrel=5
pkgdesc='Portable foreign function interface library'
arch=('x86_64' 'loongarch64')
url='https://sourceware.org/libffi/'
license=('MIT')
depends=('glibc')
checkdepends=('dejagnu')
provides=('libffi.so')
options=('debug')
_commit=70602040e7a319ad4131aad422d59a493bc65f18 # loongarch-3_4_2
source=("git+https://github.com/loongson/libffi.git#commit=$_commit")
sha256sums=('SKIP')
b2sums=('SKIP')

build() {
  cd $pkgname
  # remove --disable-exec-static-tramp once ghc and gobject-introspection
  # work fine with it enabled (https://github.com/libffi/libffi/pull/647)
  ./autogen.sh
  ./configure \
    --prefix=/usr \
    --disable-static \
    --disable-multi-os-directory \
    --disable-exec-static-tramp \
    --enable-pax_emutramp
  make
}

check() {
  make -C $pkgname-$pkgver check
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
  install -Dm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm 644 README.md -t "$pkgdir"/usr/share/doc/$pkgname
}

# vim:set ts=2 sw=2 et:
