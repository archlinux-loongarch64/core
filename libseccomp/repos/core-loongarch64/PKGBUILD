# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Patryk Kowalczyk < patryk at kowalczyk dot ws>

pkgbase=libseccomp
pkgname=(libseccomp python-libseccomp)
pkgver=2.5.3
pkgrel=3
pkgdesc='Enhanced seccomp library'
arch=('x86_64' 'loongarch64')
license=('LGPL2.1')
url="https://github.com/seccomp/libseccomp"
depends=('glibc')
checkdepends=('valgrind')
makedepends=('gperf' 'cython' 'python-setuptools')
_commit=5283a03194436a9f956c5be9370e9a30844fe3fd # dev-main
source=("git+https://github.com/loongarch64/libseccomp.git#commit=$_commit")
sha256sums=('SKIP')
b2sums=('SKIP')
validpgpkeys=(
  '7100AADFAE6E6E940D2E0AD655E45A5AE8CA7C8A' # Paul Moore <paul@paul-moore.com>
  '47A68FCE37C7D7024FD65E11356CE62C2B524099' # Tom Hromatka <tom.hromatka@oracle.com>
)

prepare() {
  cd ${pkgbase}
  autoreconf -fiv
}

build() {
  cd ${pkgbase}
  ./configure --prefix=/usr
  make
  cd src/python
  env VERSION_RELEASE=${pkgver} python setup.py build
}

check() {
  cd ${pkgbase}-${pkgver}
  make check
}

package_libseccomp() {
  provides=('libseccomp.so')
  cd ${pkgbase}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 CHANGELOG README.md SECURITY.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python-libseccomp() {
  depends=('python' 'libseccomp.so')
  cd ${pkgbase}/src/python
  env VERSION_RELEASE=${pkgver} python setup.py install --root="${pkgdir}" --prefix=/usr -O1 --skip-build
}

# vim: ts=2 sw=2 et:
